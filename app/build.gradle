apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.grs"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        flavorDimensions "default"
        multiDexEnabled true
        ndk {
            abiFilters 'x86', 'armeabi'  //使用uniapp时必须同时选择二者或选其一
        }
    }
    signingConfigs {
        config {
            keyAlias 'sign'
            keyPassword '123456'
            storeFile file('../sign.keystore')
            storePassword '123456'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            debuggable false
        }
        debug {
            debuggable true
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    //使用uniapp时，需复制下面代码
    /*代码开始*/
    aaptOptions {
        additionalParameters '--auto-add-overlay'
        //noCompress 'foo', 'bar'
        ignoreAssetsPattern "!.svn:!.git:.*:!CVS:!thumbs.db:!picasa.ini:!*.scc:*~"
    }
    /*代码结束*/
    productFlavors {
        aTest{
            applicationIdSuffix '.test'
            resValue "string", "app_name", "测试"
            buildConfigField "String", "SPLASH", "splash"
            manifestPlaceholders = [LAUNCHER: "com.grs.WelcomeActivity", BaiDu_Map_AK: "Mfzm782DpGGKIRq2PH2LyPiSuxC3z9rD", app_icon: "@drawable/icon_test"]
        }
        travel {
            // 每个环境包名不同
            applicationIdSuffix '.travel'
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "去哪儿"
            // 动态修改 常量 字段
            buildConfigField "String", "SPLASH", "splash_travel"
            // 修改 AndroidManifest.xml 里渠道变量
            manifestPlaceholders = [LAUNCHER: "io.dcloud.PandoraEntry", BaiDu_Map_AK: "", app_icon: "@drawable/icon"]
        }
        oldsland {
            applicationIdSuffix '.oldsland'
            resValue "string", "app_name", "小岛"
            buildConfigField "String", "SPLASH", '"splash_oldsland"'
            manifestPlaceholders = [LAUNCHER: "io.dcloud.PandoraEntry", BaiDu_Map_AK: "Mfzm782DpGGKIRq2PH2LyPiSuxC3z9rD", app_icon: "@drawable/icon"]
        }
    }
    // 自定义APK输出配置
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 输出apk名称为com.grs.xxx_v1.0.0_20171205_baidu.apk
                def fileName = "${defaultConfig.applicationId}.${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${getNowTime()}.apk"
                outputFileName = fileName
            }
        }
    }

}
//获取时间戳

def getNowTime() {
    def date = new Date()
    def now = date.format('yyyyMMddHHmm')
    return now
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //    添加uniapp时需要添加recycler，bindingingx-core和bindingx_weex_plugin库
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.alibaba.android:bindingx-core:1.0.3'
    implementation 'com.alibaba.android:bindingx_weex_plugin:1.0.3'
    implementation 'com.just.agentweb:agentweb:4.0.2'
}
